#include "vg_define.h"
#include "culling.h"
#include "wave.h"

layout(local_size_x = VG_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;
void main()
{
#if INSTANCE_CULL_MODE != INSTANCE_CULL_POST
	uint instanceId = gl_GlobalInvocationID.x;
#else
	uint instanceId = MainCullResult[gl_GlobalInvocationID.x];
#endif

	if (instanceId < numInstance)
	{
		uint resourceIndex = InstanceData[instanceId].resourceIndex;
		mat4 localToWorld = InstanceData[instanceId].transform;

		vec4 boundCenter = ResourceData[resourceIndex].boundCenter;
		vec4 boundHalfExtend = ResourceData[resourceIndex].boundHalfExtend;

		FrustumCullData cullData = BoxCullFrustumGeneral(boundCenter.xyz, boundHalfExtend.xyz, localToWorld, worldToClip, true, false);

#if INSTANCE_CULL_MODE == INSTANCE_CULL_MAIN || INSTANCE_CULL_MODE == INSTANCE_CULL_POST
		if (cullData.bIsVisible)
		{
			ivec2 hzbSize = textureSize(hiZTex, 0);
			ScreenRect rect = GetScreenRect(ivec4(0, 0, hzbSize * 2), cullData, 4);
			cullData.bIsVisible = IsVisibleHZB(rect, hzbSize, true);
		}
#if INSTANCE_CULL_MODE == INSTANCE_CULL_MAIN
		if (!cullData.bIsVisible)
		{
			uint offset = atomicAdd(PostCullIndirectArgs[3], 1);
			MainCullResult[offset] = instanceId;
			PostCullIndirectArgs[0] = atomicMax(PostCullIndirectArgs[0], (offset + VG_GROUP_SIZE - 1) / VG_GROUP_SIZE);
		}
#endif
#endif
		if (cullData.bIsVisible)
		{
			uint nodeWriteOffset = 0;
			INTERLOCK_ADD(QueueState[QUEUE_STATE_INDEX].nodeWriteOffset, 1, nodeWriteOffset);
			WAVE_INTERLOCK_ADD_ONLY(QueueState[QUEUE_STATE_INDEX].nodeCount, 1);
			if (nodeWriteOffset < MAX_CANDIDATE_NODE)
			{
#if INSTANCE_CULL_MODE == INSTANCE_CULL_POST
				nodeWriteOffset += MAX_CANDIDATE_NODE;
#endif
				CandidateNode node;
				node.instanceId = instanceId;
				node.nodeIndex = 0;
				StoreCandidateNode(nodeWriteOffset, node);
			}
		}
	}
}